Dir0 = "`pwd`"
Dir1 = Dir0."/postProcessing/sampleDict2/5500/"
Dir2 = Dir0."/postProcessing/sampleDict1/5500/"
Dir3 = Dir0."/postProcessing/"

cd Dir0

set term png

#
# Perform a linear regression over 5 steps in the developed flow region
# to determine the pressure drop slope
#
f(x) = a*x + b
fit f(x) Dir2.'pressDropCenterlineRibs_p.xy' u ($1*1000):($2-101325) via a, b
title_f(a,b) = sprintf('Slope %.6f Pa/mm', a)

#
# Calculate the normalized and nondimensionalized pressure drop
# in the rib-roughened section
#
Re = 20000
fo = 0.046 * Re**-0.2

D = 0.05
rho = 1.205
U = 6.06
f = -a*1000 * D/4*2/rho/U**2
f_over_fo = f/fo

#
# Calculate the bulk fluid temperature
#
mu = 1.825e-5
cp = 1007
Pr = 0.7309
flux = 1000
Tin = 20
thick = 0.001
L = 0.720

A = D*thick
mdot = rho*A*U

T(x) = (x >= 0 && x <= 0.720) ? (Tin + flux*thick*x/mdot/cp) : 1/0

#
# Calculate normalized Nusselt number
#
e = 0.005
Nuo = 0.023 * Re**0.8 * Pr**0.4
k = mu*cp/Pr
Q = flux*thick*L




set key right top
set xlabel 'x (mm)'
set ylabel 'Gage Pressure (Pa)'
set title 'Centerline Pressure Drop'
set grid
set output Dir3."midlinePressureDrop.png"
set label 1 at 500, 2 sprintf('f/f_o = %.3f', f_over_fo)
plot [][] Dir2.'pressDropCenterline_p.xy' u ($1*1000):($2-101325) w l t "OpenFoam", f(x) t title_f(a,b), Dir2.'pressDropCenterlineRibs_p.xy' u ($1*1000):($2-101325) w l t "Developed Section"


set key off
set xlabel 'x (m)'
set ylabel 'y+'
set title 'Lower Wall Turbulence Wall Function y+'
set grid
set output Dir3."lowerWallyPlus.png"
plot [][] Dir1.'lowerWall_yPlus_wallHeatFlux_T.xy' u ($1):($2) w l


set key off
set xlabel 'x (m)'
set ylabel 'q (W/m^2)'
set title 'Lower Wall Heat Flux'
set grid
set output Dir3."lowerWallHeatFlux.png"
plot [][] Dir1.'lowerWall_yPlus_wallHeatFlux_T.xy' u ($1):($3) w l


set key left top
set xlabel 'x (m)'
set ylabel 'T (deg C)'
set title 'Lower Wall Temperature'
set grid
set output Dir3."lowerWallTemp.png"
plot [][] Dir1.'lowerWall_yPlus_wallHeatFlux_T.xy' u ($1):($4-273.15) w l title "OpenFoam", T(x) t "Bulk Fluid Temp"



#
# First, calculate the normalized Nusselt number
# Second, export the data to a file
# Third, calculate the average of the data
#
set table Dir3."Nusselt.dat"
plot [][] Dir1.'lowerWall_yPlus_wallHeatFlux_T.xy' u (($1-0.485)/e):(flux*D/($4-273.15-T($1))/k/Nuo) w l
unset table

set xrange [0:11]
stats Dir3.'Nusselt.dat'


#
# Fourth, plot the data and include the average as a label
#
set key off
set xlabel 'x/e'
set xrange [0:11]
set ylabel 'Nu/Nu_o'
set title 'Normalized Nusselt Number over 9^{th} Step'
set grid
set output Dir3."Nusselt.png"
set label 1 at 4, 0.95 sprintf('Avg. Nu/Nu_o = %.3f', STATS_mean_y)
plot [][] Dir1.'lowerWall_yPlus_wallHeatFlux_T.xy' u (($1-0.485)/e):(flux*D/($4-273.15-T($1))/k/Nuo) w l
